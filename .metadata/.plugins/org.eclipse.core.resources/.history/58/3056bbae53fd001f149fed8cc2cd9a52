package com.flashiteasy.admin.workbench.impl
{
	import com.flashiteasy.admin.browser.FileManager;
	import com.flashiteasy.admin.components.CustomTree;
	import com.flashiteasy.admin.components.FileManipulationBar;
	import com.flashiteasy.admin.conf.Conf;
	import com.flashiteasy.admin.conf.Ref;
	import com.flashiteasy.admin.fieservice.FileManagerService;
	import com.flashiteasy.admin.popUp.NewPagePopUp;
	import com.flashiteasy.admin.utils.EditorUtil;
	import com.flashiteasy.admin.xml.XmlSave;
	import com.flashiteasy.api.bootstrap.AbstractBootstrap;
	import com.flashiteasy.api.core.BrowsingManager;
	import com.flashiteasy.api.core.project.Page;
	import com.flashiteasy.api.core.project.PageList;
	import com.flashiteasy.api.core.project.Project;
	import com.flashiteasy.api.utils.PageUtils;

	import flash.display.DisplayObject;
	import flash.events.Event;
	import flash.events.IOErrorEvent;

	import mx.collections.XMLListCollection;
	import mx.containers.VBox;
	import mx.controls.Label;
	import mx.controls.Tree;
	import mx.controls.buttonBarClasses.ButtonBarButton;
	import mx.core.Application;
	import mx.events.ItemClickEvent;
	import mx.events.ListEvent;


	public class NavigationTree
	{
		/**
		 * Create a navigation tree based on the project
		 * Permit to move, copy rename branches using FileManagerService
		 * @see com.flashiteasy.api.fieservice.filemanager
		 */
		private var title:Label;
		private var tree:CustomTree;
		private var pages:PageList;
		private var provider:XMLList;
		private var contentBox:VBox;
		private var toolBar:FileManipulationBar;
		private var baseURL:String;
		private var _copiedPage:Page=null;
		private var _cutedPage:Page=null;
		//variables to use later using rename instead of delete for keeping a copy of deleted pages
		private var _deletedPage:Page=null;
		private var _deletedNameList:Array;
		private var _garbageUrl:String;

		[Bindable]
		private var data:XMLListCollection=new XMLListCollection();

		private var inputName:NewPagePopUp;
		private var browserAction:FileManager;
		private var fms:FileManagerService=new FileManagerService();
		private var itemId:String;
		private var _newXML:XML;
		private var _newUuid:String;

		/**
		 * Create the tree and add listener to FileManagerService
		 *
		 */
		public function NavigationTree()
		{
			fms.addEventListener(FileManagerService.ERROR, error);
			contentBox=new VBox();
			title=new Label;
			title.width=200;
			title.setStyle("fontWeight", "bold");
			title.text=Conf.languageManager.getLanguage("Project_Pages");
			toolBar=new FileManipulationBar();
			tree=new CustomTree();
			tree.dataProvider=data;
			browserAction=new FileManager();
			tree.addEventListener(ListEvent.ITEM_CLICK, selectItem);
			tree.width=200;
			toolBar.addEventListener(ItemClickEvent.ITEM_CLICK, selectButton);
			//label
			//contentBox.addChild(title);
			//Navigation tree
			contentBox.addChild(tree);
			//file manipulation
			contentBox.addChild(toolBar);
		}

		/**
		 * Get the all component
		 *
		 * @return contentBox
		 */
		public function getUIComponent():VBox
		{
			return contentBox;

		}

		public function setTree(tree:Tree):void
		{
			//this.tree=tree;
			//tree.addEventListener(ListEvent.ITEM_CLICK, selectItem);

		}

		/**
		 * Get pages from Project
		 *
		 * @return PageList
		 */

		public function getPageList():PageList
		{
			return pages;
		}

		public function setPageList(pages:PageList):void
		{
			this.pages=pages;
		}

		/**
		 * Build the tree based on serviceURL
		 */
		public function update(expand:Boolean=false):void
		{
			baseURL=Conf.SERVICE_URL;
			buildTree();
			if (expand)
			{
				tree.callLater(tree.expandAll);
			}
		}

		//Listener for the toolBar
		private function selectButton(e:ItemClickEvent):void
		{
			var item:ButtonBarButton=toolBar.getChildAt(e.FieAdmin) as ButtonBarButton;
			var currentPage:Page=BrowsingManager.getInstance().getCurrentPage();
			itemId=toolBar.dataProvider[e.FieAdmin].id;
			switch (itemId)
			{
				case "delete":
					_deletedPage=currentPage;
					removePage();
					break;
				case "cut":
					_cutedPage=currentPage;
					_copiedPage=currentPage;
					break;
				case "copy":
					//When copiing removing _cutedPage
					_cutedPage=null;
					_copiedPage=currentPage;
					break;
				case "paste":
					//used with copy and cut
					pastePage();
					break;
				case "add":
					createNamePopUp();
					//addNewPageXML();
					break;
			}
		}

		private function createNamePopUp():void
		{
			inputName=new NewPagePopUp(mx.core.Application.application as DisplayObject, true);
			inputName.description=Conf.languageManager.getLanguage("New_Page_Name");
			inputName.label=Conf.languageManager.getLanguage("Name") + ": ";
			inputName.setInputDefaultValue(EditorUtil.findUniqueName("page", PageUtils.getPagesLink(BrowsingManager.getInstance().getCurrentPage())));
			inputName.addEventListener(NewPagePopUp.SUBMIT, checkName);
		}

		private function checkName(e:Event):void
		{
			inputName.closePopUp();
			addNewPageXML(inputName.getInput(), inputName.isSubPage(), inputName.isOnStage(), inputName.pageContainer());
		}

		//Listener for the tree
		private function selectItem(e:ListEvent):void
		{
			//In case the selected page is deselect with command or shift
			//we force a reselection
			if (e.target.selectedItem == null)
			{
				e.target.selectedIndex=e.rowIndex;
			}
			else
			{
			//	XmlSave.temporarySave();
				var s:String=createUrlFromItem();
				BrowsingManager.getInstance().showUrl(s);
			}
		}

		private function buildTree():void
		{
			data.source=buildDataProvider();
			tree.labelField="@label";
		}


		//paste a page on the project at the level of the tree selectedItem

		private function pastePage():void
		{
			var pageParent:PageList=BrowsingManager.getInstance().getCurrentPage().getParent();
			var newPage:Page=_copiedPage.clone();
			var uuid:String=_copiedPage.link;
			//var uuidArray:Array=getUuidArray(pageParent.getPages());
			_newUuid=EditorUtil.findUniqueName(uuid, PageUtils.getPagesLink(pageParent));
			//Workaround for pages at the first level cannot be cast as Page
			var pasteUrl:String=pageParent is Project ? baseURL + "/xml/" + _newUuid : baseURL + "/xml/" + Page(pageParent).getPageUrl() + "/" + _newUuid;
			var pagesToCopy:Array=[baseURL + "/xml/" + _copiedPage.getPageUrl() + ".xml"]
			var pagesToPaste:Array=[pasteUrl + ".xml"];

			//if the page have children we should add the directory of the subPages
			if (_copiedPage.getPages().length > 0)
			{
				pagesToCopy.push(baseURL + "/xml/" + _copiedPage.getPageUrl());
				pagesToPaste.push(pasteUrl);
			}

			fms.addEventListener(FileManagerService.COPY_FILE, complete);
			if (_cutedPage == null)
			{
				//we haven't cut so copying file
				fms.copyFiles(pagesToCopy, pagesToPaste);
			}
			else
			{
				//we have cut so Moving the file
				fms.renameFile(pagesToCopy, pagesToPaste);
			}

		}


		private function removePage():void
		{
			//TODO : we could move the files to a garbage URL for using undo redo action

			/*var pageToRemove:Page=BrowsingManager.getInstance().getCurrentPage();
			   baseURL + "/xml/" + pageToRemove.getPageUrl() + ".xml"];
			   var garbageArray:Array=["fie-admin/bin-debug/garbage/" + pageToRemove.getPageUrl() + ".xml"];
			   if (pageToRemove.getPages().length > 0)
			   {
			   removeArray.push(baseURL + "/xml/" + pageToRemove.getPageUrl());
			   garbageArray.push("fie-admin/bin-debug/garbage/" + pageToRemove.getPageUrl());
			   }

			   fms.addEventListener(FileManagerService.COPY_FILE, complete);
			   fms.renameFile(removeArray, garbageArray);
			 */

			var pageParent:PageList=BrowsingManager.getInstance().getCurrentPage().getParent();
			var pageToRemove:Page=BrowsingManager.getInstance().getCurrentPage();
			var removeArray:Array=[pageToRemove.getPageUrl() + ".xml"];

			//If pageParent have only one child directory should be remove
			if (!(pageParent is Project) && Page(pageParent).getPages().length == 1)
			{
				removeArray.push(Page(pageParent).getPageUrl());
			}

			//if the page have children we should add the directory of the subPages
			if (pageToRemove.getPages().length > 0)
			{
				removeArray.push(pageToRemove.getPageUrl());
			}
			fms.addEventListener(FileManagerService.DELETE_FILE, complete);
			fms.deleteFiles(baseURL + "/xml", removeArray);
		}

		private function complete(e:Event):void
		{
			var pageParent:PageList;
			var newPage:Page;
			var pageToRemove:Page;
			switch (itemId)
			{
				case "delete":
					fms.removeEventListener(FileManagerService.DELETE_FILE, complete);
					pageParent=BrowsingManager.getInstance().getCurrentPage().getParent();
					pageParent.removePage(BrowsingManager.getInstance().getCurrentPage());
					saveProjectPagesXML();
					BrowsingManager.getInstance().getCurrentPage().hide();
					break;
				case "paste":
					fms.removeEventListener(FileManagerService.COPY_FILE, complete);
					pageParent=BrowsingManager.getInstance().getCurrentPage().getParent();
					newPage=_copiedPage.clone();
					newPage.link=_newUuid;
					newPage.setParent(pageParent);
					pageParent.addPage(newPage);
					if (_cutedPage != null)
					{
						pageToRemove=_cutedPage;
						pageParent=_cutedPage.getParent();
						pageParent.removePage(pageToRemove);
					} // else {
					_cutedPage=null;
					saveProjectPagesXML();
					//}
					break;
				case "add":
					fms.removeEventListener(FileManagerService.PAGE_CREATED, complete);
					if (inputName.isSubPage())
					{
						pageParent=BrowsingManager.getInstance().getCurrentPage();
					}
					else
					{
						pageParent=BrowsingManager.getInstance().getCurrentPage().getParent();
					}
					newPage=new Page(pageParent);
					newPage.link=_newUuid;
					newPage.container=inputName.pageContainer();
					pageParent.addPage(newPage);
					saveProjectPagesXML();

					break;
			}
		}


		private function addNewPageXML(name:String, isSubpage:Boolean, isOnStage:Boolean, container:String):void
		{
			var pageParent:Page;
			_newUuid=name;
			fms.addEventListener(FileManagerService.PAGE_CREATED, complete);
			if (isSubpage)
			{
				pageParent=BrowsingManager.getInstance().getCurrentPage();
				fms.createPage(baseURL + "/xml/" + pageParent.getPageUrl(), _newUuid);
			}
			else
			{
				if (BrowsingManager.getInstance().getCurrentPage().parentIsPage)
				{
					pageParent=Page(BrowsingManager.getInstance().getCurrentPage().getParent());
					fms.createPage(baseURL + "/xml/" + pageParent.getPageUrl(), _newUuid);
				}
				else
				{
					fms.createPage(baseURL + "/xml", _newUuid);
				}
			}
		}


		private function saveProjectPagesXML():void
		{
			_newXML=AbstractBootstrap.getInstance().getXML();
			_newXML.replace("pages", getPagesXml());
			fms.addEventListener(FileManagerService.FILE_SAVED, saveSuccess, false, 0, true);
			fms.saveContent(baseURL + "/xml/project.xml", _newXML);
		}

		private function error(e:Event):void
		{
			trace("error with fileManager");
		}


		private function saveError(e:IOErrorEvent):void
		{
			trace("couldn t find php");
		}

		private function saveSuccess(e:Event):void
		{
			trace("XML saved");
			/*var project:Project=ProjectParser.parseProject(_newXML);
			   BrowsingManager.getInstance().setProject(project);
			 pages=project;*/
			update();
		}

		/*
		 * UTILITIES Functions
		 *
		 */
		//Return the list ofUUID at the same level

		private function getUuidArray(pgs:Array):Array
		{
			var Ar:Array=[];
			for (var i:int=0; i < pgs.length; i++)
			{
				Ar.push(pgs[i].link);
			}
			return Ar;
		}

		//Return real URL from a selectedItem
		private function createUrlFromItem():String
		{
			var item:Object=tree.selectedItem;
			var Ar:Array=[tree.selectedItem.@label];
			var parent:Object=tree.getParentItem(tree.selectedItem);
			while (parent != null)
			{
				Ar.unshift(parent.@label);
				item=parent;
				parent=tree.getParentItem(item);
			}
			return Ar.join("/");
		}

		//Return a XMLList base on Project
		public function buildDataProvider():XMLList
		{
			var pageList:Array=pages.getPages();
			var page:Page;
			var pageNode:String="<root>";

			for each (page in pageList)
			{
				pageNode+=getPageXML(page);
			}
			pageNode+="</root>";
			provider=new XMLList(new XML(pageNode).children());
			return provider;
		}

		//return XML String for each page
		private function getPageXML(page:Page):String
		{
			if (page.getPages().length > 0)
			{
				var childPage:Page
				var pageNode:String="";
				pageNode="<node label='" + page.link + "' >";
				for each (childPage in page.getPages())
				{
					pageNode+=getPageXML(childPage);
				}
				pageNode+="</node>";
				return pageNode;
			}
			else
			{
				return "<node label='" + page.link + "' />";
			}
			return "";
		}

		//Rebuild the pages portion of the project.xml
		private function getPagesXml():String
		{
			var pageList:Array=pages.getPages();
			var page:Page;
			var pageNode:String="<root>";

			pageNode+="<pages>";
			for each (page in pageList)
			{

				pageNode+=serializePage(page);
			}
			pageNode+="</pages></root>";
			var pagesXML:XMLList=new XMLList(new XML(pageNode).children());
			return pagesXML;

		}

		//return XML for a page
		private function serializePage(page:Page):String
		{
			if (page.getPages().length > 0)
			{
				var childPage:Page
				var pageNode:String="";
				pageNode='<page link="' + page.link + '"';
				if (page.container != null && page.container != "stage")
				{
					pageNode+=' container = "' + page.container + '"';
				}
				pageNode+='>';
				for each (childPage in page.getPages())
				{
					pageNode+=serializePage(childPage);
				}
				pageNode+="</page>";
				return pageNode;
			}
			else
			{
				var simplePage:String='<page link="' + page.link + '"';
				if (page.container != null && page.container != "stage")
				{
					simplePage+=' container = "' + page.container + '"';
				}
				simplePage+=' />';
				return simplePage;
			}
			return "";
		}


	}
}