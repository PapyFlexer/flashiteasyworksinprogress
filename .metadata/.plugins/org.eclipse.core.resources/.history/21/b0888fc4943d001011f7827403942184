/**
 * FLASHITEASY API 1.0 : Framework for visual interactive developpement <http://www.flashiteasy.com/>
 *
 * FLASHITEASY API 1.0 is (c) 2008-2011 by Robert DEVOS, Didier REYT, Gilles ROQUEFEUIL & Dany SIRIPHOL
 * This software is released under the MIT License <http://www.opensource.org/licenses/mit-license.php/>
 * 
 *
 */

package com.flashiteasy.api.parameters
{
	import com.flashiteasy.api.core.AbstractParameterSet;
	import com.flashiteasy.api.core.BrowsingManager;
	import com.flashiteasy.api.core.IDescriptor;
	import com.flashiteasy.api.core.IParameterSetStaticValues;
	import com.flashiteasy.api.core.action.IPlayStoryAction;
	import com.flashiteasy.api.core.project.Page;
	import com.flashiteasy.api.core.project.storyboard.Story;
	import com.flashiteasy.api.core.project.storyboard.StoryTypes;

	// metadata
    [ParameterSet(description="Play_Story",type="Reflection", groupname="Story")]
	/**
	 * The <code><strong>PlayStoryParameterSet</strong></code> is the parameterSet
	 * that handles the action of triggering an animation (Story).
	 * The metadata generates a combobox listing the possible targets (stories)
	 * on stage.
	 */
	public class PlayStoryParameterSet extends AbstractParameterSet  implements IParameterSetStaticValues
	{
		
		private var _storiesToPlay:Array;
		
        /**
         * 
         * @inheritDoc
         */
        override public function apply(target:IDescriptor):void
        {
            //super.apply( target );
            if ( target is IPlayStoryAction )
            {
                IPlayStoryAction( target ).setStoriesToTrigger(_storiesToPlay , target.getPage() ); 
            }
        }
 
       [Parameter(type="List", defaultValue="",row="0", sequence="0", label="Story")]
		/**
		 * the Array of stories to play
		 */
		public function get storiesToPlay():Array
		{
			return _storiesToPlay;
		}

		/**
		 * 
		 * @private
		 */
		public function set storiesToPlay(value:Array):void
		{
			_storiesToPlay=value;
		}
		
		/**
		 * Returns the array of 
		 * @param name
		 * @return 
		 */
		public function getPossibleValues(name:String):Array
		{
			var page : Page = new Page;
			//page = IDescriptor(this).getPage()
			page = BrowsingManager.getInstance().getCurrentPage();
			var stories:Array = page.getStoryArray();
			var storyLabels : Array = new Array;
		
			for each (var story : Story in stories)
			{
				// lists only the triggered stories
				if ( story.autoPlay == false) storyLabels.push(story.uuid);
			}
			return storyLabels;
		}
		
	}
}